// solves wordle puzzle using known words
// Terminal setup: transmit CR for newline, local echo off, vt24
#include "optiboot.h"
// flash
#define NUMBER_OF_PAGES 1   // number of pages of 128 , one page contains 25 worte then terminated by \0xx
// allocate flash to write to, must be initialized, one more than used as 0 for EOS
const uint8_t flashSpace[SPM_PAGESIZE * (NUMBER_OF_PAGES + 1)] __attribute__ (( aligned(SPM_PAGESIZE) )) PROGMEM = {"\x0"}; //

#include "thesau58.h"

// 128 bytes are
struct  wort_s {
  char eintr [25][5];
  uint8_t wnull;
  uint8_t wb;
  uint8_t wc;
} ;

typedef union {
  wort_s worte;
  byte ramBuffer[SPM_PAGESIZE];
} chunk_t ;
chunk_t chunk;

char buchs[26]; // +,-,' '
char ist[6];
char muss[6];
char exac[6];
byte exacP;

enum stateT {norm, plus, minus, leer, exakt};
stateT state;


uint16_t inp;
bool inpAkt = false;
unsigned long currMs, prevMs = 0;
bool verbo = true;
bool plumi = true;


void prnt(PGM_P p) {
  while (1) {
    char c = pgm_read_byte(p++);
    if (c == 0) break;
    Serial.write(c);
  }
  Serial.write(" ");
}

void msgF(const __FlashStringHelper *ifsh, uint16_t n) {
  // text verbraucht nur flash
  PGM_P p = reinterpret_cast<PGM_P>(ifsh);
  prnt(p);
  Serial.println(n);
}

void readPage(uint16_t page) {
  msgF(F("readPage"), page);
  if ((page > 0) && (page <= NUMBER_OF_PAGES)) {  // page is translated -1 so eg 10 reads [9]
    optiboot_readPage(flashSpace, chunk.ramBuffer, page);
  } else {
    msgF(F("err: Page max "), NUMBER_OF_PAGES);
  }
}

void writePage(uint16_t page) {
  msgF(F("writePage"), page);
  if ((page > 0) && (page <= NUMBER_OF_PAGES)) {
    optiboot_writePage(flashSpace, chunk.ramBuffer, page);
  } else {
    msgF(F("err: Page max "), NUMBER_OF_PAGES);
  }
}

void copyBeis(uint8_t  n) {
  if (n < anzBeis) {
    byte i = 0;
    byte b = !0;
    while (b != 0) {
      b = pgm_read_byte( &beis[n][i]);
      chunk.ramBuffer[i++] = b;
    }
  }
}

void initBuchs() {
  memset (buchs, ' ', 26);
  memset (exac, '*', 5);
}

void showBuchs() {
  uint8_t i;
  char str[20];
  for ( i = 0; i < 26; i++)   {
    if (i % 13 == 0)  {
      Serial.println();
    }
    sprintf(str, "%1c %1c  ", char(i + 'a'), buchs[i]);
    Serial.print(str);
  } // next i
  Serial.println();
  for ( i = 0; i < 5; i++)   {
    Serial.write(exac[i]);
  }
  Serial.println();
  prepareMuss();
}

void prepareMuss() {
  byte m = 0;
  for ( byte i = 0; i < 26; i++)   {
    if (buchs[i] == '+' ) {
      muss[m] = i + 'a';
      m++;
      if (m > 4) break;
    }
  } // next
  muss[m] = 0;
  // show
  for ( byte i = 0; i < 6; i++)   {
    if (muss[i] == 0) {
      Serial.println("<");
      return;
    }
    Serial.write(muss[i]);
    Serial.write(' ');
  }
  Serial.println("??");
}

void showChunk() {
  for ( byte i = 0; i < 128; i++)   {
    if (i % 80 == 0) Serial.println();
    if (chunk.ramBuffer[i] == 0) {
      Serial.println("<<");
      return;
    }
    Serial.write(chunk.ramBuffer[i]);
    if (i % 5 == 4) Serial.write(' ');
  } //next
}

bool checkMuss() {
  // true if ist and muss
  bool found;
  for ( byte m = 0; m < 5; m++) {
    if (muss[m] == 0 ) return true;
    found = false;
    for ( byte i = 0; i < 5; i++) {
      if (muss[m] == ist[i]) {
        found = true;
        break;
      }
    } // next i
    if (!found) return false;
  } // next j
  return true;
}

bool checkExac() {
  // true if ist and exac
  for ( byte i = 0; i < 5; i++) {
    if (exac[i] != '*' ) {
      if (exac[i] != ist[i]) return false;
    }
  } // next i
  return true;
}

bool checkIst() {
  if (checkMuss()) {
    if (checkExac()) {
      if (verbo) {
        Serial.write('!');
      } else {
        for ( byte j = 0; j < 5; j++)   {
          Serial.write(ist[j]);
        }
        Serial.write(' ');
        return true;
      } // found
    } else {
      if (verbo) Serial.write('E');
      return false;
    }
  } else { // muss fail
    if (verbo) Serial.write('M');
    return false;
  }
  if (verbo) Serial.write(' ');
}

void checkChunk() {
  byte ic = 0;
  char c;
  for ( byte i = 0; i < 128; i++)   {
    c = chunk.ramBuffer[i];
    if (c == 0) {
      Serial.println("<<");
      return;
    }
    if (verbo) Serial.write(c);
    if (buchs[c - 'a'] == '-') { // not allowed, jump to next wort
      i += (4 - ic);
      if (verbo) Serial.write('#');
      ic = 0;
    } else {
      ist[ic] = c;
      ic++;
      if (ic > 4) { //wort in ist  ist zulässig
        checkIst();
        ic = 0;
      } // zulässig
    } // allowed
  } //next
}

void doLoop(uint16_t n) {
  unsigned long anfMs, endMs;
  prepareMuss();
  anfMs = millis();
  if (n == 0) n = anzBeis;
  if (n > anzBeis) n = anzBeis;
  for ( byte b = 0; b < n; b++) {
    copyBeis(b);
    checkChunk();
  }
  endMs = millis();
  Serial.print("Pages ");
  Serial.print(n);
  Serial.print(" took ");
  Serial.println(endMs - anfMs);
}

// syntax
// -abc  verbieten
// +abc  muss
// #abc  egal
// =a_b__     exakt 5!
// 0..9 zu inp
// w
// b beispiel -> flash
// r
// d suche laut drin
// e suche laut exac
// n suche laut nidri

void prompt() {
  state = norm;
  Serial.print("N>");
}

void doCmd(byte tmp) {
  if ((tmp >= '0') && (tmp <= '9')) {
    if (inpAkt) {
      inp = inp * 10 + (tmp - '0');
    } else {
      inpAkt = true;
      inp = tmp - '0';
    }
    Serial.print("\b\b\b\b");
    Serial.print(inp);
    return;
  }
  inpAkt = false;
  Serial.print(char(tmp));


  switch (tmp) {
    case '+':   //
      state = plus;
      return;
    case '-':   //
      state = minus;
      return;
    case '#':   //
      state = leer;
      return;
    case '=':   //
      state = exakt;
      exacP = 0;
      return;
    case 13:
      prompt();
      return;
  } // switch control

  if (state == exakt) {
    if (exacP > 4) {
      Serial.println("<<");
      prompt();
      return;
    }
    if ( tmp == '*') {
      exac[exacP++] = tmp;
      return;
    }
  }

  if (tmp <'a' or tmp >'z') {
    Serial.println("nicht a-z");
    prompt();
    return;
  }
  if (state == plus) {
    buchs[tmp - 'a'] = '+';
    return;
  }
  if (state == minus) {
    buchs[tmp - 'a'] = '-';
    return;
  }
  if (state == leer) {
    buchs[tmp - 'a'] = ' ';
    return;
  }
  if (state == exakt) {
    exac[exacP++] = tmp;
    return;
  }
  switch (tmp) {
    case 'b':   //
      copyBeis(inp);
      showChunk();
      break;
    case 'c':   //
      showChunk();
      break;
    case 'i':   //
      initBuchs();
      break;
    case 'l':   //
      Serial.println("oop");
      prepareMuss();
      doLoop(inp);
      break;
    case 'p':   //
      Serial.println("ruf");
      prepareMuss();
      checkChunk();
      break;
    case 's':   //
      showBuchs();
      break;
    case 'r':   //
      readPage(inp);
      break;
    case 'w':   //
      writePage(inp);
      break;
    case 'v':   //
      verbo = !verbo;
      Serial.print("erbose ");
      if (verbo) {
        Serial.println("an");
      } else {
        Serial.println("aus");
      }
      break;
    default:
      Serial.print(tmp);
      Serial.println ("?  0..5, +, -, show, verbose");
  } //case
  prompt();
}

void setup() {
  const char info[] = "wort5 " __DATE__  " " __TIME__;
  Serial.begin(38400);
  Serial.println(info);
  msgF(F("chunk"), sizeof(chunk));
  msgF(F("flash"), sizeof(flashSpace));
  msgF(F("beisp"), sizeof(beis));
  initBuchs();
  prompt();
}


void loop() {
  if (Serial.available() > 0) {
    doCmd( Serial.read());
  } // serial

  currMs = millis();
  if (currMs - prevMs > 1000) {
    prevMs = currMs;
  } // timer
}
